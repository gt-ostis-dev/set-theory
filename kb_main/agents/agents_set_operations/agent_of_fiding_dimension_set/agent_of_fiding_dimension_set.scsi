agent_of_finding_dimension_set
=> nrel_main_idtf:
	[агентная scp-программа поиска размерности множеств] 
	(* <- lang_ru;; *);

	[agent scp-program of finding dimension set] 
	(* <- lang_en;; *);

<- agent_scp_program;;

scp_program -> agent_of_finding_dimension_set 
	(*
	-> rrel_params: .agent_of_finding_dimension_set_params 
		(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
		*);;

	->rrel_operators: .agent_of_finding_dimension_set_operator_set (*

		->rrel_init: .agent_of_finding_dimension_set_operator1 (*
			<- searchElStr3;;
			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _quest;;
			=>nrel_goto: .agent_of_finding_dimension_set_operator2;;
		*);;

		-> .agent_of_finding_dimension_set_operator2 (*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: question_finding_dimension_set;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;
			=>nrel_then: .agent_of_finding_dimension_set_operator3;;
			=>nrel_else: .agent_of_finding_dimension_set_operator_return;;
		*);;
		// Найдём параметры операции – их подал на вход сам пользователь
		-> .agent_of_finding_dimension_set_operator3 (*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _param;;
			=>nrel_then: .agent_of_finding_dimension_set_operator5;;
			=>nrel_else: .agent_of_finding_dimension_set_operator_return;;
		*);;
		-> .agent_of_finding_dimension_set_operator5 (*
			<- call;; // В первом параметре содержится название вызванной процедуры
			-> rrel_1: rrel_fixed: rrel_scp_const:  proc_of_finding_dimension_set ;;
			->rrel_2:rrel_fixed:rrel_scp_const: // здесь описаны параметры, подаваемые на
			// вход процедуре
			.agent_of_finding_dimension_set_operator5_params (*
				-> rrel_1: rrel_fixed: rrel_scp_var: _param;;
				-> rrel_2: rrel_assign: rrel_scp_var: _curr;;
			*);;
			// Знак scp-процесса, который может быть использован для того, чтобы
			// дождаться завершения созданного scp-процесса
			-> rrel_3: rrel_assign: rrel_scp_var: _descr;;
			=>nrel_goto: .agent_of_finding_dimension_set_operator6;;
		*);;
		// scp-оператор ожидания завершения выполнения программы
		-> .agent_of_finding_dimension_set_operator6 (*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;
			=>nrel_goto: .agent_of_finding_dimension_set_operator7;;
		*);;

		-> .agent_of_finding_dimension_set_operator7 (*
            <- varAssign;;
            -> rrel_1: rrel_assign: rrel_scp_var: _var_dimension_set;;
            -> rrel_2: rrel_fixed: rrel_scp_const: nrel_dimension_set;;
            => nrel_goto: .agent_of_finding_dimension_set_operator8;;
        *);;

        -> .agent_of_finding_dimension_set_operator8 (*
            <- call;;
            -> rrel_1: rrel_fixed: rrel_scp_const: proc_add_five_elements;;
            -> rrel_2: ... (*
                -> rrel_1: rrel_fixed: rrel_scp_var: _param;;
                -> rrel_2: rrel_fixed: rrel_scp_var: _var_dimension_set;;
                -> rrel_3: rrel_fixed: rrel_scp_var: _curr;;
                -> rrel_4: rrel_assign: rrel_scp_var: _answer;;
            *);;
            -> rrel_3: rrel_assign: rrel_scp_var: _argumentq;;
            => nrel_goto: .agent_of_finding_dimension_set_operator9;;
        *);;

        -> .agent_of_finding_dimension_set_operator9 (*
            <- waitReturn;;
            -> rrel_1: rrel_fixed: rrel_scp_var: _argumentq;;
            => nrel_goto: .agent_of_finding_dimension_set_operator_gen_answer;;
        *);;

		// Добавляем узел с конструкцией ответа
		-> .agent_of_finding_dimension_set_operator_gen_answer (*
			<- genElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;
			=>nrel_goto: .agent_of_finding_dimension_set_operator_return;;
		*);;
		// Оператор завершения агентной операции
		-> .agent_of_finding_dimension_set_operator_return (*
			<- return;;
		*);;
	*);;
*);;
